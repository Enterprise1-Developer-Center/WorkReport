apply plugin: 'com.android.application'
apply plugin: 'hugo'

def keystorePropertiesFile = rootProject.file("keystore.properties")
// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {

  signingConfigs {
    release {
      keyAlias keystoreProperties['keyAlias']
      keyPassword keystoreProperties['keyPassword']
      storeFile file(keystoreProperties['storeFile'])
      storePassword keystoreProperties['storePassword']
    }
  }

  compileSdkVersion rootProject.compile_sdk_version
  buildToolsVersion rootProject.build_tools_version
  defaultConfig {
    applicationId rootProject.application_id
    minSdkVersion rootProject.min_sdk_version
    targetSdkVersion rootProject.target_sdk_version
    versionCode rootProject.version_code

    int versionMajor = 0
    int versionMinor = 0
    int versionPatch = rootProject.version_code

    if (1000 > versionPatch && versionPatch > 99) {
      versionMajor = versionPatch / 100
      versionMinor = (versionPatch % 100) / 10
      versionPatch = (versionPatch % 100) % 10
    } else if (100 > versionCode && versionPatch > 9) {
      versionMajor = 0
      versionMinor = versionPatch / 10
      versionPatch = versionPatch % 10
    } else if (versionPatch < 10) {
      versionMajor = 0
      versionMinor = 0
      versionPatch = versionPatch / 1
    }
    versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      signingConfig signingConfigs.release
      minifyEnabled false
      proguardFile getDefaultProguardFile('proguard-android.txt')
      proguardFile 'proguard-rules.pro'
      proguardFile 'proguard-butterknife.pro'
      proguardFile 'proguard-retrofit.pro'
      proguardFile 'proguard-okhttp.pro'
    }
    debug {
      minifyEnabled false
      proguardFile getDefaultProguardFile('proguard-android.txt')
      proguardFile 'proguard-rules.pro'
      proguardFile 'proguard-butterknife.pro'
      proguardFile 'proguard-retrofit.pro'
      proguardFile 'proguard-okhttp.pro'
    }
  }
  packagingOptions {
    pickFirst 'META-INF/ASL2.0'
    pickFirst 'META-INF/LICENSE'
    pickFirst 'META-INF/NOTICE'
  }
  repositories {
    maven {
      url 'https://maven.google.com'
      // Alternative URL is 'https://dl.google.com/dl/android/maven2/'
    }
  }
  compileOptions {
    targetCompatibility JavaVersion.VERSION_1_8
    sourceCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
    exclude group: 'com.google.code.findbugs'
  })
  compile "com.android.support:appcompat-v7:$support_version"
  compile "com.android.support:support-v4:$support_version"
  compile "com.android.support:design:$support_version"
  compile 'com.android.support.constraint:constraint-layout:1.0.2'
  //compile "com.android.support:preference-v14:$support_version"

  testCompile 'junit:junit:4.12'

  compile "com.ibm.mobile.foundation:ibmmobilefirstplatformfoundation:$mfp_version"

  compile "com.jakewharton:butterknife:$butterknife_version"
  annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_version"

  compile "com.google.dagger:dagger:$dagger_version"
  compile "com.google.dagger:dagger-android-support:$dagger_version"
  annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

  compile "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
  compile "io.reactivex.rxjava2:rxjava:$rxjava_version"
  compile "com.jakewharton.rxbinding2:rxbinding:$rxbinding_version"

  compile "com.jakewharton.timber:timber:$timber_version"

  compile "org.projectlombok:lombok:$lombok_version"
  annotationProcessor "org.projectlombok:lombok:$lombok_version"
  provided "javax.annotation:jsr250-api:$jsr_version"

  compile "com.github.PhilJay:MPAndroidChart:$MPAndroidChart_version"

  compile "com.google.code.gson:gson:$gson_version"

  debugCompile "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
  releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"

  compile "com.squareup.retrofit2:retrofit:$retrofit_version"
  compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
  compile "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

  compile "com.squareup.okhttp3:logging-interceptor:$okhttp3_version"
  compile "com.squareup.okhttp3:okhttp:$okhttp3_version"

  compile "com.pixplicity.easyprefs:library:$easyprefs_version"

  compile "jp.wasabeef:recyclerview-animators:$recyclerview_animators"
}
repositories {
  mavenCentral()
}
